import org.apache.tools.ant.taskdefs.condition.Os
apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: "com.jfrog.bintray"

group = 'kr.co.smartstudy'
version = '0.1.3'


android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 23
        versionCode 1
        versionName version
    }
    buildTypes {
        release {
            consumerProguardFiles 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            jni.srcDirs = []
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
}


def getNdkBuildCmd() {
    if (System.env.ANDROID_NDK_ROOT != null)
        return System.env.ANDROID_NDK_ROOT
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def ndkdir = properties.getProperty('ndk.dir', null)
    if (ndkdir == null)
        throw new GradleException("NDK location not found. Define location with ndk.dir in the local.properties file or with an ANDROID_NDK_ROOT environment variable.")

    def ndkbuild = ndkdir + "/ndk-build"
    if (Os.isFamily(Os.FAMILY_WINDOWS))
        ndkbuild += ".cmd"

    return ndkbuild
}


def getNDKBuildArguments(boolean doClean) {


    def String arg0 = "NDK_APP_LIBS_OUT=src/main/jniLibs"
    def String arg1 = "NDK_OUT=build/tmpobj"
    def String arg2 = "NDK_PROJECT_PATH=src/main"

    def List<String> args = ['-C',file('.').absolutePath,arg0,arg1,arg2]
    if (doClean)
        args.push("clean")

    return args.toArray()

}

task "buildNative_${project.name}"(group: 'ndk') << {
    exec {
        executable getNdkBuildCmd()
        args getNDKBuildArguments(false)
    }
}

task "cleanNative_${project.name}"(group: 'ndk') <<  {
    exec {
        executable getNdkBuildCmd()
        args getNDKBuildArguments(true)
    }
}


ext {
    bintrayRepo = 'maven'
    bintrayName = 'soundpoolcompat'

    publishedGroupId = group
    libraryName = 'SoundPoolCompat'
    artifact = 'soundpoolcompat'

    libraryDescription = 'Android SoundPoolCompat Library using OpenSLES'

    siteUrl = 'https://github.com/barikang/SoundPoolCompat'
    gitUrl = 'https://github.com/barikang/SoundPoolCompat.git'

    libraryVersion = version

    developerId = 'barikang'
    developerName = 'Jaehun Ha'
    developerEmail = 'barikang@gmail.com'

    licenseName = 'The MIT License (MIT)'
    licenseUrl = 'http://opensource.org/licenses/mit-license.php'
    allLicenses = ["MIT"]
}


install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            artifactId = artifact;
            project {
                packaging 'aar'
                name libraryName
                description = libraryDescription

                licenses {
                    license {
                        name licenseName
                        url licenseUrl
                    }
                }
                developers {
                    developer {
                        id developerId
                        name developerName
                        email developerEmail
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())


bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        publish = true
        publicDownloadNumbers = true

        version {
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = properties.getProperty("bintray.gpg.password") //Optional. The passphrase for GPG signing'
            }
//            mavenCentralSync {
//                sync = true //Optional (true by default). Determines whether to sync the version to Maven Central.
//                user = properties.getProperty("bintray.oss.user") //OSS user token
//                password = properties.getProperty("bintray.oss.password") //OSS user password
//                close = '1' //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
//            }
        }
    }
}
